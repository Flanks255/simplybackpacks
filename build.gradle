buildscript {
    repositories {
        maven { url = 'https://maven.creeperhost.net/' }
        maven { url = 'https://maven.minecraftforge.net' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '4.1.+', changing: true
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: "com.matthewprenger.cursegradle"
apply from: 'https://files.saps.dev/ci/git-md-changelog.gradle'

repositories {
    maven { url "https://www.cursemaven.com" }
    maven { url "https://dvs1.progwml6.com/files/maven" }
}

def ENV = System.getenv()
version = "${mod_version}-build.${ENV.GITHUB_RUN_NUMBER ?: 9999}"
group = 'com.flanks255'
archivesBaseName = 'simplybackpacks'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

minecraft {
    mappings channel: 'snapshot', version: "$mappings_version"

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.console.level', 'debug'

            mods {
                simplybackpacks {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            property 'forge.logging.console.level', 'debug'

            args 'nogui'

            mods {
                simplybackpacks {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'forge.logging.console.level', 'debug'

            args '--mod', 'simplybackpacks', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                simplybackpacks {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

dependencies {
    minecraft "net.minecraftforge:forge:$forge_version"

    compileOnly fg.deobf("mezz.jei:jei-${jei_version}:api")
    runtimeOnly fg.deobf("mezz.jei:jei-${jei_version}")

    compile fg.deobf("curse.maven:TOP-245211:3157997")
    compile fg.deobf("curse.maven:BUILDINGGADGETS-298187:3205295")
    compile fg.deobf("curse.maven:NBTEDIT-247580:3062106")
    compile fg.deobf("curse.maven:SIMPLYLIGHT-300331:3288992")
    compile fg.deobf("curse.maven:MINGAD-351748:3279909")
    compile fg.deobf("curse.maven:CHARGEGAD-399757:3055493")

}

jar {
    manifest {
        attributes([
            "Specification-Title": "simplybackpacks",
            "Specification-Vendor": "Flanks255",
            "Specification-Version": "1", // We are version 1 of ourselves
            "Implementation-Title": project.name,
            "Implementation-Version": "${version}",
            "Implementation-Vendor" :"Flanks255",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

def reobfFile = file("$buildDir/reobfJar/output.jar")
def reobfArtifact = artifacts.add('default', reobfFile) {
    type 'jar'
    builtBy 'reobfJar'
}
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact(reobfArtifact) {
                builtBy reobfArtifact
                artifactId = archivesBaseName
                classifier null
            }
        }
    }

    repositories {
        if (ENV.SAPS_TOKEN) {
            maven {
                url "https://maven.saps.dev/minecraft"
                credentials {
                    username = "flanks"
                    password = "${ENV.SAPS_TOKEN}"
                }
            }
        }
    }
}

if (ENV.CURSEFORGE_KEY) {
    curseforge {
        apiKey = ENV.CURSEFORGE_KEY
        project {
            id = project.curseforge_id
            releaseType = "beta"
            addGameVersion "Forge"
            addGameVersion "1.16.5"
            addGameVersion "1.16.4"
            addGameVersion "1.16.3"
            addGameVersion "1.16.2"
            addGameVersion "1.16.1"
            mainArtifact(jar)
            changelog = getGitChangelog
            changelogType = 'markdown'
        }
    }
}