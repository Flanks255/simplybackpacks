buildscript {
    repositories {
        maven { url = 'https://maven.creeperhost.net/' }
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = 'https://maven.parchmentmc.org' }
        maven { url = "https://plugins.gradle.org/m2/" }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'org.parchmentmc:librarian:1.+'
        classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.4.0"
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.parchmentmc.librarian.forgegradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: "com.matthewprenger.cursegradle"
apply from: 'https://files.saps.dev/ci/git-md-changelog.gradle'

repositories {
    maven { url "https://www.cursemaven.com" }
    maven { url = "https://maven.theillusivec4.top/" }
    maven { url "https://dvs1.progwml6.com/files/maven" }
    maven { url = "https://maven.saps.dev/minecraft" }
}

def ENV = System.getenv()
version = "${minecraft_version}-${mod_version}-build.${ENV.GITHUB_RUN_NUMBER ?: 9999}"
group = 'com.flanks255'
archivesBaseName = 'simplybackpacks'

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

minecraft {
    mappings channel: 'parchment', version: "$mappings_version"

    //accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')

            args '--width', '1280', '--height', '720'

            mods {
                simplybackpacks {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            args 'nogui'

            mods {
                simplybackpacks {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            args '--mod', 'simplybackpacks', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                simplybackpacks {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

dependencies {
    minecraft "net.minecraftforge:forge:$forge_version"

    compileOnly fg.deobf("mezz.jei:jei-${jei_version}:api")
    runtimeOnly fg.deobf("mezz.jei:jei-${jei_version}")

    runtimeOnly fg.deobf("com.flanks255:simplylight:1.16.4-1.+")

    runtimeOnly fg.deobf("curse.maven:TOP-245211:3157997")
    runtimeOnly fg.deobf("curse.maven:NBTEDIT-247580:3062106")
    runtimeOnly fg.deobf("curse.maven:ENSORC-349447:3407021")
    runtimeOnly fg.deobf("curse.maven:COFHCORE-69162:3407019")
    runtimeOnly fg.deobf("curse.maven:BG-298187:3498508")

    runtimeOnly fg.deobf("top.theillusivec4.curios:curios-forge:${curios_version}")
    compileOnly fg.deobf("top.theillusivec4.curios:curios-forge:${curios_version}:api")
}

jar {
    manifest {
        attributes([
                "Specification-Title": "simplybackpacks",
                "Specification-Vendor": "Flanks255",
                "Specification-Version": "1", // We are version 1 of ourselves
                "Implementation-Title": project.name,
                "Implementation-Version": "${version}",
                "Implementation-Vendor" :"Flanks255",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

def reobfFile = file("$buildDir/reobfJar/output.jar")
def reobfArtifact = artifacts.add('default', reobfFile) {
    type 'jar'
    builtBy 'reobfJar'
}
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact(reobfArtifact) {
                builtBy reobfArtifact
                artifactId = archivesBaseName
                classifier null
            }
        }
    }

    repositories {
        if (ENV.SAPS_TOKEN) {
            maven {
                url "https://maven.saps.dev/minecraft"
                credentials {
                    username = "flanks"
                    password = "${ENV.SAPS_TOKEN}"
                }
            }
        }
    }
}

if (ENV.CURSEFORGE_KEY) {
    curseforge {
        apiKey = ENV.CURSEFORGE_KEY
        project {
            id = project.curseforge_id
            releaseType = "release"
            addGameVersion "Forge"
            addGameVersion "1.16.5"
            mainArtifact(jar)
            changelog = getGitChangelog
            changelogType = 'markdown'
        }
    }
}