plugins {
    id 'java-library'
    id 'idea'
    id 'maven-publish'
    id 'net.neoforged.gradle.userdev' version '7.0.84'
    id "me.modmuss50.mod-publish-plugin" version "0.4.5"
}

repositories {
    maven { url = 'https://maven.creeperhost.net/' }
    maven { url = 'https://www.cursemaven.com' }
    maven { url = 'https://maven.theillusivec4.top/' }
    maven { url = 'https://maven.saps.dev/releases' }
}

def ENV = System.getenv()
version = "${minecraft_version}-${mod_version}-build.${ENV.GITHUB_RUN_NUMBER ?: 9999}"
group = 'com.flanks255'
base {
    archivesName = "simplybackpacks"
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

sourceSets.main.resources { srcDir 'src/generated/resources' }
minecraft.accessTransformers.file rootProject.file('src/main/resources/META-INF/accesstransformer.cfg')

runs {
    // applies to all the run configs below
    configureEach {
        //Limit ram usage for the dev environment to 4GB
        jvmArgument '-Xmx8G'

        modSource project.sourceSets.main
    }

    client {
        systemProperty 'neoforge.enabledGameTestNamespaces', "simplybackpacks"
        programArguments.addAll('--width', '1280', '--height', '720')
        if  (ENV.MCDEVUSER) {
            programArguments.addAll('--username', ENV.MCDEVUSER)
        }
        if (ENV.MCDEVUUID) {
            programArguments.addAll('--uuid', ENV.MCDEVUUID)
        }
    }

    server {
        systemProperty 'neoforge.enabledGameTestNamespaces', "simplybackpacks"
        programArgument '--nogui'
    }

    gameTestServer {
        systemProperty 'forge.enabledGameTestNamespaces', "simplybackpacks"
    }

    data {
        programArguments.addAll '--mod', "simplybackpacks", '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
    }
}

dependencies {
    implementation "net.neoforged:neoforge:${neo_version}"
    compileOnly "mezz.jei:jei-${minecraft_version}-common-api:${jei_version}"
    compileOnly "mezz.jei:jei-${minecraft_version}-neoforge-api:${jei_version}"
    runtimeOnly "mezz.jei:jei-${minecraft_version}-neoforge:${jei_version}"

    //runtimeOnly fg.deobf("com.flanks255:simplylight:1.19.3-1.4.+")

    //runtimeOnly fg.deobf("curse.maven:TOP-245211:3671753")

    runtimeOnly "top.theillusivec4.curios:curios-neoforge:${curios_version}"
    compileOnly "top.theillusivec4.curios:curios-neoforge:${curios_version}:api"
}

jar {
    manifest {
        attributes([
                "Specification-Title": "simplybackpacks",
                "Specification-Vendor": "Flanks255",
                "Specification-Version": "1", // We are version 1 of ourselves
                "Implementation-Title": project.name,
                "Implementation-Version": "${version}",
                "Implementation-Vendor" :"Flanks255",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

    publishing {
        publications {
            register('mavenJava', MavenPublication) {
                groupId project.group
                artifactId base.archivesName.get()
                version project.version
                from components.java
            }
        }

        repositories {
            if (ENV.SAPS_TOKEN) {
                maven {
                    url "https://maven.saps.dev/minecraft"
                    credentials {
                        username = "flanks"
                        password = "${ENV.SAPS_TOKEN}"
                    }
                }
            }
        }
    }
//https://modmuss50.github.io/mod-publish-plugin/
    publishMods {
        dryRun = !providers.environmentVariable("CURSEFORGE_KEY").isPresent()
        file = jar.archiveFile
        changelog = file("./CHANGELOG.md").text
        modLoaders.add("NeoForge")
        type = STABLE
        curseforge {
            accessToken = providers.environmentVariable("CURSEFORGE_KEY")
            projectId = project.curseforge_id
            minecraftVersions.add(project.minecraft_version)
        }
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
    }